plugins {
    id 'java'
    id "org.springframework.boot" version "2.1.6.RELEASE"
    id 'application'
    id 'war'
    id 'maven-publish'
}

group 'com.wxmlabs.hellogradle'
version '1.0-SNAPSHOT'

configurations {
    bom
    implementation.extendsFrom(bom)
    providedCompile.extendsFrom(bom)
    providedRuntime.extendsFrom(bom)
    developmentOnly
    runtimeClasspath.extendsFrom(developmentOnly)
}

dependencies {
    bom enforcedPlatform("org.springframework.boot:spring-boot-dependencies:2.1.6.RELEASE")
    implementation 'org.springframework.boot:spring-boot-starter-web'
    providedCompile 'javax.servlet:javax.servlet-api'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

springBoot {
    buildInfo()
}

application {
    mainClassName = 'com.wxmlabs.hellogradle.springboot2.App'
    applicationDefaultJvmArgs = ['-Dfile.encoding=UTF-8']
    distZip.enabled = false
    distTar.enabled = false
}

publishing{
    publications {
        "$project.name"(MavenPublication) {
            artifact bootWar
        }
    }
}
