plugins {
    id 'java-platform'
    id 'maven-publish'
}

repositories {
    jcenter()
}

dependencies {
    constraints {
        // Tools
        def lombokVersion = "1.18.8"
        api "org.projectlombok:lombok:${lombokVersion}"
        // API, SDK
        api "com.alibaba:fastjson:1.2.61"
        api "com.google.guava:guava:27.0.1-jre"
        api "commons-codec:commons-codec:1.12"
        api "commons-io:commons-io:2.6"
        api "org.apache.commons:commons-pool2:2.6.2"
        def bcVersion = "1.62"
        api "org.bouncycastle:bcpkix-jdk15on:${bcVersion}"
        api "org.bouncycastle:bcprov-jdk15on:${bcVersion}"
        api "org.bouncycastle:bcprov-debug-jdk15on:${bcVersion}"
        api "org.codehaus.groovy:groovy-all:2.5.6"
        // Test
        def spockVersion = "1.2-groovy-2.5"
        api "org.spockframework:spock-core:${spockVersion}"
        api "org.spockframework:spock-spring:${spockVersion}"
    }
    api enforcedPlatform("org.springframework.boot:spring-boot-dependencies:2.1.6.RELEASE")
}

javaPlatform {
    allowDependencies()
}

publishing {
    publications {
        "${project.name}"(MavenPublication) {
            artifactId = "${rootProject.name}-${project.name}"
            from components.javaPlatform
            versionMapping {
                allVariants {
                    fromResolutionResult()
                }
            }
        }
    }
    repositories {
        maven {
            if (project.version.toString().endsWith("SNAPSHOT")) {
                url 'http://localhost:8081/repository/maven-snapshots/'
            } else {
                url 'http://localhost:8081/repository/maven-releases/'
            }
            credentials {
                username 'admin'
                password 'password'
            }
        }
    }
}
